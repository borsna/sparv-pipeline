from sparv import malt, annotate

_malt_model = "%s/swemalt-1.7.2.mco" % sparv_model_dir
_malt_jar = "%s/maltparser-1.7.2/maltparser-1.7.2.jar" % sparv_bin_dir

rule _malt1:
    "Do dependency parsing with malt on {file}."
    input:
        TEXT = "{file}.@TEXT",
        token = "{file}.token",
        sentence = "{file}.sentence",
        word = "{file}.token.word",
        pos = "{file}.token.pos",
        msd = "{file}.token.msd",
        model = _malt_model,
        maltjar = _malt_jar
    output:
        "{file}.token.malt"
    run:
        malt.maltparse(
            out=output[0],
            word=input.word,
            sentence=input.sentence,
            pos=input.pos,
            msd=input.msd,
            text=input.TEXT,
            token=input.token,
            model=input.model,
            maltjar=input.maltjar
        ),

rule _malt2:
    "Make annotations for 'dependency relation' and 'dependency head' from token.malt on {file}."
    input:
        "{file}.token.malt",
    output:
        deprel = "{file}.token.deprel",
        dephead = "{file}.token.dephead"
    run:
        annotate.select(
            out=output.deprel,
            annotation=input[0],
            index=0
        ),
        annotate.select(
            out=output.dephead,
            annotation=input[0],
            index=1
        )

rule malt:
    "Do dependency parsing with malt on all files."
    input:
        expand("%s/{file}.token.deprel" % annotation_dir, file=input_files),
        expand("%s/{file}.token.dephead" % annotation_dir, file=input_files)
