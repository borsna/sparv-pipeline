from sparv import hunpos, annotate


hunpos_model = "%s/hunpos.suc3.suc-tags.default-setting.utf8.model" % sparv_model_dir
hunpos_morphtable = "%s/hunpos.saldo.suc-tags.morphtable" % sparv_model_dir
hunpos_morphtable_swedalin = "%s/hunpos.dalinm-swedberg.saldo.suc-tags.morphtable" % sparv_model_dir
hunpos_morphtable_patterns = "%s/hunpos.suc.patterns" % sparv_model_dir


rule _hunpos_msd:
    "Do part-of-speech tagging with hunpos on {file}."
    input:
        TEXT = "{file}.@TEXT",
        word = "{file}.token.word",
        sentence = "{file}.sentence",
        model = hunpos_model,
        morphtable = hunpos_morphtable_swedalin if analysis == "sv-1800" else hunpos_morphtable,
        patterns = [] if analysis == "sv-1800" else hunpos_morphtable_patterns
    output:
        "{file}.token.msd"
    run:
        hunpos.msdtag(
            out=output[0],
            text=input.TEXT,
            word=input.word,
            sentence=input.sentence,
            model=input.model,
            morphtable=input.morphtable,
            patterns=input.patterns
        )


rule hunpos_msd:
    "Do part-of-speech tagging with hunpos on all files."
    input:
        expand("%s/{file}.token.msd" % annotation_dir, file=input_files)


rule _pos_from_msd:
    """Extract part-of-speech from MSD for {file}."""
    input:
        "{file}.token.msd"
    output:
        "{file}.token.pos"
    run:
        annotate.select(
            out=output[0],
            annotation=input[0],
            index=0,
            separator="."
        )

rule pos_from_msd:
    "Extract part-of-speech from MSD for all files."
    input:
        expand("%s/{file}.token.pos" % annotation_dir, file=input_files)
