from sparv import cwb, number
import os


rule _vrt:
    "Export {file} to vrt format."
    params:
        annotations_columns = ["{file}.%s" % i[0] for i in _positional_annotations],
        annotations_structs = ["{file}.%s" % i[0] for i in _structural_annotations]
    input:
        ["{file}.%s" % i[0] for i in _positional_annotations],
        ["{file}.%s" % i[0] for i in _structural_annotations],
        TEXT = "{file}.@TEXT",
        token = "{file}.token",
        token_order = "{file}.token.n"
    output:
        "{file}.vrt"
    run:
        cwb.export(
            format="vrt",
            out=output[0],
            order=input.token_order,
            annotations_columns=params.annotations_columns,
            annotations_structs=params.annotations_structs,
            columns=[i[1] for i in _positional_annotations],
            structs=[i[1] for i in _structural_annotations],
            text=input.TEXT,
            token=input.token
        )


rule vrt:
    "Export all input files to vrt format."
    input:
        expand(os.path.join(annotation_dir, "{file}.vrt"), file=input_files)


rule _export:
    "Export {file} to xml."
    params:
        annotations_columns = ["%s/{filename}.%s" % (annotation_dir, i[0]) for i in _positional_annotations],
        annotations_structs = ["%s/{filename}.%s" % (annotation_dir, i[0]) for i in _structural_annotations],
        fileid = "{filename}"
    input:
        ["%s/{filename}.%s" % (annotation_dir, i[0]) for i in _positional_annotations],
        ["%s/{filename}.%s" % (annotation_dir, i[0]) for i in _structural_annotations],
        TEXT = os.path.join(annotation_dir, "{filename}.@TEXT"),
        token = os.path.join(annotation_dir, "{filename}.token"),
        token_order = os.path.join(annotation_dir, "{filename}.token.n"),
        fileids = os.path.join(annotation_dir, "fileids")
    output:
        os.path.join(export_dir, "{filename}.xml")
    run:
        cwb.export(
            format="xml",
            out=output[0],
            order=input.token_order,
            annotations_columns=params.annotations_columns,
            annotations_structs=params.annotations_structs,
            columns=[i[1] for i in _positional_annotations],
            structs=[i[1] for i in _structural_annotations],
            text=input.TEXT,
            token=input.token,
            fileid=params.fileid,
            fileids=input.fileids
        )


rule export:
    "Export all input files to xml."
    input:
        expand(os.path.join(export_dir, "{file}.xml"), file=input_files)
